pkgname=alttab-git
_name=alttab
pkgver=v1.4.0.21.g7e4fc4c
pkgrel=2
pkgdesc="The task switcher for minimalistic window managers or standalone X11 session"
url="https://github.com/sagb/alttab"
license=('GPL3')
arch=('i686' 'x86_64')
depends=('libx11' 'libxmu' 'libxft' 'libxrender' 'libxrandr' 'libpng' 'uthash' 'libxpm')
makedepends=('autoconf' 'automake' 'git')
source=("git+https://github.com/sagb/alttab.git#branch=master")
sha1sums=('SKIP')

pkgver() {
	cd "$_name"
	git describe --tags |sed 's/-/./g'
}

build() {
	cd "$_name"
	export CFLAGS="${CFLAGS} -fcommon"
	export LDFLAGS="${LDFLAGS} -fcommon"
	./bootstrap.sh
	./configure --prefix=/usr
	make
}

package() {
	cd "$_name"
	make DESTDIR="$pkgdir/" install
}
